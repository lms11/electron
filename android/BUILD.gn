# Copyright 2024 The Electron Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//third_party/icu/config.gni")
import("//third_party/jni_zero/jni_zero.gni")

electron_manifest =
    "$target_gen_dir/electron_manifest/AndroidManifest.xml"

generate_jni("content_shell_jni_headers") {
  sources = [
    "java/src/org/chromium/content_shell/ElectronShell.java",
    "java/src/org/chromium/content_shell/ElectronShellManager.java",
  ]
}

shared_library_with_jni("libelectron_content_view") {
  testonly = true
  deps = [
    ":content_shell_jni_headers",
    "//components/crash/content/browser",
    "//content/shell:content_shell_app",
    "//content/shell:content_shell_lib",
    "//content/shell:pak",
    "//media",
    "//skia",
  ]

  # Explicit dependency required for JNI registration to be able to
  # find the native side functions.
  if (is_component_build) {
    deps += [
      "//device/gamepad",
      "//media/midi",
    ]
  }
  sources = [ "electron_library_loader.cc" ]
  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [ "//build/config/android:hide_all_but_jni" ]
  java_targets = [
    ":electron_apk",
    ":content_shell_java",
  ]
}

android_resources("electron_apk_resources") {
  testonly = true
  sources = [
    "apk/res/drawable/ic_refresh.png",
    "apk/res/layout/electron_activity.xml",
    "apk/res/layout/shell_view.xml",
    "apk/res/mipmap-hdpi/app_icon.png",
    "apk/res/mipmap-mdpi/app_icon.png",
    "apk/res/mipmap-xhdpi/app_icon.png",
    "apk/res/mipmap-xxhdpi/app_icon.png",
    "apk/res/mipmap-xxxhdpi/app_icon.png",
    "apk/res/values/strings.xml",
  ]
}

android_library("content_shell_java") {
  testonly = true
  
  resources_package = "org.chromium.content_shell"
  deps = [
    ":electron_apk_resources",
    "//base:base_java",
    "//components/embedder_support/android:content_view_java",
    "//components/embedder_support/android:view_java",
    "//content/public/android:content_java",
    "//third_party/android_deps:com_google_code_findbugs_jsr305_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]
  
  sources = [
    "java/src/org/chromium/content_shell/ElectronShell.java",
    "java/src/org/chromium/content_shell/ElectronShellManager.java",
  ]
  
  srcjar_deps = [ ":content_shell_jni_headers" ]
}

android_library("electron_apk_java") {
  testonly = true
  
  resources_package = "org.electronjs.apk"
  deps = [
    ":content_shell_java",
    ":electron_apk_resources",
    ":electron_manifest",
    "//base:base_java",
    "//base:process_launcher_java",
    "//build/android:build_java",
    "//components/embedder_support/android:view_java",
    "//content/public/android:content_java",
    "//media/capture/video/android:capture_java",
    "//net/android:net_java",
    "//third_party/android_deps:com_google_code_findbugs_jsr305_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]

  sources = [
    "apk/src/org/electronjs/apk/ElectronActivity.java",
    "apk/src/org/electronjs/apk/ElectronApplication.java",
    "apk/src/org/electronjs/apk/AssetExtractor.java",
  ]
}

android_assets("electron_apk_assets") {
  testonly = true
  sources = [
    "apk/assets/index.html",
    "apk/assets/renderer.js",
  ]
}

android_assets("electron_assets") {
  testonly = true
  sources = [ "$root_out_dir/content_shell.pak" ]
  disable_compression = true
  deps = [
    "//content/shell:pak",
    "//gin:v8_snapshot_assets",
    "//third_party/icu:icu_assets",
  ]
}

jinja_template("electron_manifest") {
  testonly = true
  input = "apk/AndroidManifest.xml.jinja2"
  output = electron_manifest
  variables = [ "manifest_package=org.electronjs.apk" ]
}

android_apk("electron_apk") {
  testonly = true
  apk_name = "Electron"
  android_manifest = electron_manifest
  android_manifest_dep = ":electron_manifest"
  deps = [
    ":electron_apk_assets",
    ":electron_apk_java",
    ":electron_assets",
    "//base:base_java",
    "//components/crash/android:java",
    "//components/crash/core/app:chrome_crashpad_handler_named_as_so",
    "//components/metrics:metrics_java",
    "//content/public/android:content_java",
    "//content/public/test/android:android_test_message_pump_support_java",
    "//media/capture/video/android:capture_java",
    "//net/android:net_java",
    "//services/shape_detection:shape_detection_java",
    "//third_party/mesa_headers",
    "//ui/android:ui_java",
  ]
  shared_libraries = [ ":libelectron_content_view" ]
  srcjar_deps = [ ":libelectron_content_view__jni_registration" ]
  loadable_modules = [ "$root_out_dir/libchrome_crashpad_handler.so" ]
  command_line_flags_file = "electron-command-line"
}
